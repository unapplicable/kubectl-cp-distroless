plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-compress:1.20'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

jar {
    manifest {
        attributes(
            'Main-Class': 'io.github.unapplicable.kubectlCpDistroless.javatar.Javatar'
        )
    }
}

shadowJar {
    minimize()
}

tasks.register('addSheBang') {
    tasks.getByName('shadowJar').outputs.files.each {
        outputs.file(it)
    }

    doLast {
        outputs.files.each {
            def tmp = new File(it.getAbsolutePath() + ".tmp")

            tmp.withOutputStream { os ->
                os << "#!/usr/bin/java -jar\n"
                it.withInputStream { is -> is.transferTo(os) }
            }

            tmp.renameTo(it)
        }
    }

    dependsOn('shadowJar')
}

jar.dependsOn(addSheBang)

publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            version rootProject.version
            artifactId rootProject.name
            from components.java
        }
    }
}